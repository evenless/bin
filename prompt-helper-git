#!/usr/bin/env perl
# Add git status information to prompt

# The status is represented in the form of the word git followed by a + or -.
#
# Of the word git+/- the red characters represent:
# g = untracked files
# i = unstaged files
# t = staged files
# + = branch is ahead of master = unpushed changes
# - = branch is behind master

use strict;
use warnings;
no warnings 'uninitialized';

my $red      = '\[\e[38;5;9m\]';
my $gray     = '\[\e[38;5;243m\]';
my $green    = '\[\e[38;5;2m\]';
my $blue     = '\[\e[38;5;6m\]';
my $no_color = '\[\e[33;0;m\]';

# test data
my $git = <<'git';
# On branch masterx
# Your branch is up-to-date with 'origin/master'.
# Your branch is ahead of 'origin/master' by 1 commit.
# Untracked files
# Changes not staged for commit
# Changes to be committed
# Your branch and 'origin/master' have diverged,
git

$git = `git status -b 2>/dev/null` || exit;

$git =~ s/^# //gm;

my ($branch)   = $git =~ /^On branch (.+)$/gm;
my ($revision) = $git =~ /^HEAD detached at (.+)$/gm;

$branch ||= "?" . $revision;

my ($remote) = $git =~ /^Your branch is.* '(.+?)'/gm;
$remote =~ s/^origin\///g;

my $tracking = 1;
$tracking = 0 if !$remote;

$remote = "" if $remote eq $branch;

$branch = "" if $branch eq "master";

my ($branch_status) = $git =~ /^Your branch is (\w+) of/gm;

if ($branch_status eq "ahead") {
    my ($commit_count) =
        $git =~ /^Your branch is ahead of .* by (\d+) commit/gm;
    $branch_status = "+" . $commit_count;
}
elsif ($branch_status) {
    $branch_status = "-";
}

my $untracked    = $gray;
my $unstaged     = $gray;
my $staged       = $gray;
my $conflicts    = $gray;
my $branch_color = $gray;
$branch_color = $blue if !$tracking;

$untracked = $red       if $git =~ /^Untracked files/m;
$unstaged  = $red       if $git =~ /^Changes not staged for commit/m;
$staged    = $red       if $git =~ /^Changes to be committed/m;
$conflicts = $red . "!" if $git =~ /^Your branch .*have diverged/m;
$branch_status = $red . $branch_status if $branch_status;

$branch = " " . $branch_color . $branch if $branch || $remote;
$remote = "@" . $red . $remote if $remote;

print " "
    . $untracked . "g"
    . $unstaged . "i"
    . $staged . "t"
    . $branch_status
    . $conflicts
    . $branch
    . $remote
    . $no_color;
