#!/usr/bin/env perl
# Add git status information to prompt

# The status is represented in the form of the word git followed by a + or -.
#
# Of the word git+/- the red characters represent:
# g = untracked files
# i = unstaged files
# t = staged files
# + = branch is ahead of master = unpushed changes
# - = branch is behind master

use strict;
use warnings;
no warnings 'uninitialized';

my $red      = '\[\e[38;5;9m\]';
my $gray     = '\[\e[38;5;243m\]';
my $no_color = '\[\e[33;0;m\]';

# test data
my $git = <<'git';
# On branch masterx
# Your branch is ahead of 'origin/master' by 1 commit.
# Untracked files
# Changes not staged for commit
# Changes to be committed
# Your branch and 'origin/master' have diverged,
git

$git = `git status 2>/dev/null` || exit;

$git =~ s/^# //gm;

my ($branch) = $git =~ /^On branch (.+)$/gm;
my ($revision) = $git =~ /^HEAD detached at (.+)$/gm;
$branch ||= "?" . $revision;

if ($branch eq "master") {
    $branch = "";
}
else {
    $branch = $gray . "@" . $red . $branch . $no_color;
}

my ($branch_status) = $git =~ /^Your branch is (\w+) of/gm;

if ($branch_status eq "ahead") {
    $branch_status = "+";
}
elsif ($branch_status) {
    $branch_status = "-";
}

if ($branch_status) {
    $branch_status = $gray . $red . $branch_status . $no_color;
}

my $untracked = $gray;
my $unstaged  = $gray;
my $staged    = $gray;
my $conflicts = $gray;

$untracked = $red if $git =~ /^Untracked files/m;
$unstaged  = $red if $git =~ /^Changes not staged for commit/m;
$staged    = $red if $git =~ /^Changes to be committed/m;
$conflicts = $red . "!" if $git =~ /^Your branch .*have diverged/m;

print " "
    . $untracked . "g"
    . $unstaged . "i"
    . $staged . "t"
    . $branch_status
    . $conflicts
    . $no_color
    . $branch;
