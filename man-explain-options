#!/usr/bin/env perl

# Display man page infos about command line options

use strict;
use warnings;
no warnings 'uninitialized';

my $cl = join( " ", @ARGV ) || die "Specify command line.";
my ( $command, $options ) = $cl =~ /^(\S+) (.+)$/;
die "Specify options." if !$options;

my @options            = $options =~ /(-{1,2}\S+)/g;
my @normalized_options = ();

foreach my $option (@options) {

    # split grouped options (starting with a single dash)
    if ( $option =~ /^-[^-]{2,}/g ) {
        foreach my $single_option ( split( //, $option ) ) {
            next if $single_option eq "-";
            push( @normalized_options, "-" . $single_option );
        }
    }
    else {
        push( @normalized_options, $option );
    }
}

@options = @normalized_options;

die "No options found." if ! @options;

my $man = `man $command` || die "Cannot open man page for $command";

my @unknown_options = ();

print "$command:\n";

foreach my $option (@options) {

    my $option_regex = $option;

    if($option =~ /^--(\w+)/) {
        my $bare_option = $1;
        $bare_option =~ s/^no//g;
        $option_regex .= "|--no" . $bare_option . "|--\\[no\\]" . $bare_option . "|-no-" . $bare_option;
    }

    $option_regex = "(" . $option_regex . ")";

    # TODO: support:
    # -c, --count
    # --color-lineno=color
    # ack -s -H --nocolor --nogroup --column --smart-case --follow --count --flush --with-filename -c
    # --nocolor
    # --[no]color, --[no]colour
    # -H, --with-filename

    # print "checking option: $option / $option_regex\n";
    my ($space, $desc) = $man =~ /^(\s+)(?:(?:-{1,2}\w+, )*|\s+)($option_regex\W.+?)^\1-/ms;

    if ( !$desc ) {
        push( @unknown_options, $option );
        next;
    }
    $desc =~ s/\n$//gms;
    print "   $desc\n";
}

print STDERR "No description found for options: "
    . join( ", ", @unknown_options ) . "\n"
    if @unknown_options;
